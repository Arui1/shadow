set(shadow_cpu_src)
set(shadow_gpu_src)
set(shadow_examples_src)
set(shadow_server_src)
set(shadow_client_src)
set(shadow_test_src)
set(shadow_tools_src)

add_subdirectory(core)
add_subdirectory(examples)
add_subdirectory(operators)
add_subdirectory(proto)
add_subdirectory(server)
add_subdirectory(tools)
add_subdirectory(util)

if (${BUILD_TEST})
  add_subdirectory(test)
endif ()

include_directories(".")

if (CUDA_FOUND)
  cuda_add_library(shadow ${shadow_cpu_src} ${shadow_gpu_src})
else ()
  add_library(shadow ${shadow_cpu_src})
endif ()
target_link_libraries(shadow ${Shadow_LINKER_LIBS})
if (${USE_Protobuf} AND Protobuf_FOUND)
  target_link_libraries(shadow proto ${Protobuf_LIBRARIES})
endif ()
if (MSVC)
  set_target_properties(shadow PROPERTIES DEBUG_POSTFIX "d")
endif ()
install(TARGETS shadow DESTINATION lib)
install(DIRECTORY core operators proto server util DESTINATION include/shadow FILES_MATCHING PATTERN "*.hpp" PATTERN "*.h")

if (BUILD_SHARED_LIBS)
  set(Shadow_LIB shadow)
else ()
  shadow_add_whole_archive_flag(shadow Shadow_LIB)
endif ()

if (${BUILD_EXAMPLES})
  add_executable(test_demo "examples/test_demo.cpp" ${shadow_examples_src})
  target_link_libraries(test_demo ${Shadow_LIB})
  install(TARGETS test_demo DESTINATION bin)
endif ()

if (${BUILD_TOOLS} AND Protobuf_FOUND)
  add_executable(caffe2shadow ${shadow_tools_src})
  target_compile_definitions(caffe2shadow PRIVATE -DUSE_Protobuf)
  target_link_libraries(caffe2shadow proto ${Protobuf_LIBRARIES})
  if (${USE_GLog})
    target_link_libraries(caffe2shadow glog)
  endif ()
  install(TARGETS caffe2shadow DESTINATION bin)
endif ()

if (${BUILD_SERVICE} AND Protobuf_FOUND AND gRPC_FOUND)
  foreach (server_src ${shadow_server_src})
    get_filename_component(server_name ${server_src} NAME_WE)
    add_executable(${server_name} ${server_src} ${shadow_examples_src})
    target_compile_definitions(${server_name} PRIVATE -DBUILD_SERVICE)
    target_link_libraries(${server_name} ${Shadow_LIB} grpc_proto ${gRPC_LIBRARIES} ${Protobuf_LIBRARIES})
    install(TARGETS ${server_name} DESTINATION bin)
  endforeach ()

  foreach (client_src ${shadow_client_src})
    get_filename_component(client_name ${client_src} NAME_WE)
    add_executable(${client_name} ${client_src})
    target_link_libraries(${client_name} grpc_proto ${gRPC_LIBRARIES} ${Protobuf_LIBRARIES})
    install(TARGETS ${client_name} DESTINATION bin)
  endforeach ()
endif ()

if (${BUILD_TEST})
  add_executable(test_shadow ${shadow_test_src})
  target_link_libraries(test_shadow ${Shadow_LIB})
  if (NOT MSVC)
    target_link_libraries(test_shadow pthread)
  endif ()
  install(TARGETS test_shadow DESTINATION bin)
endif ()
