set(shadow_cpu_src)
set(shadow_gpu_src)
set(shadow_examples_src)
set(shadow_test_src)
set(shadow_tools_src)

add_subdirectory(core)
add_subdirectory(examples)
add_subdirectory(layers)
add_subdirectory(proto)
add_subdirectory(server)
add_subdirectory(test)
add_subdirectory(tools)
add_subdirectory(util)

include_directories(".")

if (CUDA_FOUND)
  cuda_add_library(shadow ${shadow_cpu_src} ${shadow_gpu_src})
else ()
  add_library(shadow ${shadow_cpu_src})
endif ()
target_link_libraries(shadow ${Shadow_LINKER_LIBS})
if (${USE_Protobuf} AND Protobuf_FOUND)
  target_link_libraries(shadow proto ${Protobuf_LIBRARIES})
endif ()
if (MSVC)
  set_target_properties(shadow PROPERTIES DEBUG_POSTFIX "d")
endif ()
install(TARGETS shadow DESTINATION lib)
install(DIRECTORY core layers proto util DESTINATION include/shadow FILES_MATCHING PATTERN "*.hpp" PATTERN "*.h")

add_executable(test_demo "examples/test_demo.cpp" ${shadow_examples_src})
target_link_libraries(test_demo shadow)
install(TARGETS test_demo DESTINATION bin)

if (${BUILD_SERVICE} AND Protobuf_FOUND)
  add_executable(adas_server "server/adas_server.cpp" ${shadow_examples_src})
  target_compile_definitions(adas_server PRIVATE -DBUILD_SERVICE)
  target_link_libraries(adas_server shadow grpc_proto grpc++_unsecure ${Protobuf_LIBRARIES})
  install(TARGETS adas_server DESTINATION bin)

  add_executable(adas_client "server/adas_client.cpp")
  target_link_libraries(adas_client grpc_proto grpc++_unsecure ${Protobuf_LIBRARIES})
  install(TARGETS adas_client DESTINATION bin)
endif ()

if (Protobuf_FOUND)
  add_executable(caffe2shadow ${shadow_tools_src})
  target_compile_definitions(caffe2shadow PRIVATE -DUSE_Protobuf)
  target_link_libraries(caffe2shadow proto ${Protobuf_LIBRARIES})
  if (${USE_GLog})
    target_link_libraries(caffe2shadow glog)
  endif ()
  install(TARGETS caffe2shadow DESTINATION bin)
endif ()

if (${BUILD_TEST})
  add_executable(test_shadow ${shadow_test_src})
  target_link_libraries(test_shadow shadow)
  if (NOT MSVC)
    target_link_libraries(test_shadow pthread)
  endif ()
  install(TARGETS test_shadow DESTINATION bin)
endif ()
